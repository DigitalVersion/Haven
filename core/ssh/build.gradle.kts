plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.hilt)
    alias(libs.plugins.ksp)
    alias(libs.plugins.gradle.test.retry)
}

android {
    namespace = "sh.haven.core.ssh"
    compileSdk = 37

    defaultConfig {
        minSdk = 26
        consumerProguardFiles("consumer-rules.pro")
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    testOptions {
        unitTests.isReturnDefaultValues = true
    }

}

dependencies {
    api(libs.jsch)
    // Second SSH engine (#58). implementation, NOT api — no module outside
    // core:ssh may see sshlib types, same rule as ChannelSftp before it.
    implementation(libs.sshlib) {
        // sshlib ships JVM tink; Haven already carries tink-android (same
        // classes, same package) — keeping both is a duplicate-class error.
        exclude(group = "com.google.crypto.tink", module = "tink")
    }
    // JSch optional deps — compileOnly so R8 doesn't error on missing classes
    compileOnly("org.slf4j:slf4j-api:2.0.18")
    compileOnly("net.java.dev.jna:jna:5.14.0")
    implementation(libs.core.ktx)
    implementation(project(":core:data"))
    implementation(project(":core:security"))
    implementation(project(":core:reticulum"))
    implementation(project(":core:mosh"))
    implementation(project(":core:et"))
    implementation(project(":core:btserial"))
    implementation(project(":core:bleserial"))
    implementation(project(":core:usbserial"))
    implementation(project(":core:local"))
    implementation(project(":core:rclone"))
    implementation(project(":core:rdp"))
    implementation(project(":core:smb"))
    implementation(project(":core:mail"))
    implementation(project(":core:fido"))
    implementation(libs.bouncycastle)
    implementation(libs.coroutines.core)
    implementation(libs.coroutines.android)
    implementation(libs.lifecycle.process)
    implementation(libs.hilt.android)
    ksp(libs.hilt.compiler)

    testImplementation(libs.junit)
    testImplementation(libs.mockk)
    testImplementation(libs.coroutines.test)
    // Embedded SSH server used to reproduce the v4.51.0 TOFU bypass bug (#75 follow-up)
    testImplementation(libs.sshd.core)
    testImplementation(libs.sshd.sftp)
    testRuntimeOnly(libs.slf4j.simple)
}

kotlin {
    compilerOptions {
        jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17)
    }
}

// Mitigate flaky test SshlibExecContractTest due to upstream sshlib CHANNEL_CLOSE race (#448)
tasks.withType<Test> {
    retry {
        maxRetries.set(2)
        maxFailures.set(20)
        failOnPassedAfterRetry.set(true)
    }
}
